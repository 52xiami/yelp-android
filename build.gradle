apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.yelp.clientlib'
archivesBaseName = "yelp-android"
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

// Set up integrationTest task to execute tests in src/integration-test separately from main build.
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    // Set upToDateWhen to false so integration tests always run when the task is called.
    outputs.upToDateWhen { false }
}

// Makes integration tests not run by default.
check.dependsOn -= integrationTest

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Yelp Android Clientlib'
                packaging 'jar'
                description 'A Java library for the Yelp API. It simplifies the process of authentication, request construction, and response parsing for Java/Android developers using the Yelp API.'
                url 'https://www.yelp.com/developers'

                scm {
                    connection 'scm:git:git@github.com:Yelp/yelp-android.git'
                    developerConnection 'scm:git:git@github.com:Yelp/yelp-android.git'
                    url 'git@github.com:Yelp/yelp-android.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.auto.value:auto-value:1.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-jackson:2.0.0-beta2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'se.akerfeldt:okhttp-signpost:1.0.0'
    compile 'com.squareup.okhttp:okhttp:2.7.0'

    testCompile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.6.3'
    testCompile 'com.squareup.okhttp:mockwebserver:2.7.0'
    testCompile 'org.easymock:easymock:3.4'
    testCompile 'org.powermock:powermock-api-easymock:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'junit:junit:4.12'
    testCompile 'junit-addons:junit-addons:1.4'
}
